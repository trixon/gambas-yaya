' Gambas class file

Property Read DicePanel As DicePanel
Property Read Panel As Panel

Private $hDicePanel As DicePanel
Private $hPanel As Panel
Private $hScorecard As CScorecard
Private $hGameDef As CGameDef

Private $iRollCounter As Integer

Private $mnuUndo As Menu

Public Sub _new(hContainer As Container, hUndo As Menu)

  $hPanel = New Panel(hContainer) As "Panel"
  $mnuUndo = hUndo
  With $hPanel
    .Arrangement = Arrange.Vertical
    .Expand = True
  End With

  $hScorecard = New CScorecard($hPanel) As "Scorecard"
  $hDicePanel = New DicePanel($hPanel) As "DicePanel"

  DicePanel.ReverseMouseScroll = MSettings.ReverseMouseScroll

End

Public Sub DicePanel_RollFinished()

  Inc $iRollCounter
  $mnuUndo.Enabled = False
  $hScorecard.RollFinished($hDicePanel.Values)
  $hDicePanel.Enabled = $iRollCounter < $hGameDef.Rolls

End

Public Sub DicePanel_RollRequest()

  $hScorecard.Undoable = False
  $hScorecard.NewRoll()
  $hDicePanel.Roll()

End

Public Sub Close()

  $hDicePanel.Close()
  Debug

End

Public Sub NewGame()

  $hGameDef = MGameLoader.GameDefs[MSettings.GameId]
  $mnuUndo.Enabled = False
  $hScorecard.NewGame()

  With $hDicePanel
    .DiceCount = $hGameDef.Dice
    .NewGame()
    .NewTurn()
  End With

End

Public Sub Open()

  $hDicePanel.Open()

End

Public Sub Settings()

  $hScorecard.Settings()

End

Public Sub Undo()

  $hScorecard.Undo()

End

Private Function Panel_Read() As Panel

  Return $hPanel

End

Private Function DicePanel_Read() As DicePanel

  Return $hDicePanel

End

Public Sub Scorecard_Register()

  $iRollCounter = 0
  $hDicePanel.NewTurn()
  $mnuUndo.Enabled = True

End

Public Sub Scorecard_Undo()

  $mnuUndo.Enabled = False
  $hDicePanel.Undo()

End

Public Sub Scorecard_GameOver()

  $hDicePanel.GameOver()

End
