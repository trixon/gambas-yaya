' Gambas class file

Public Sub Form_Open()

  lbxPlayers.List = MSettings.PlayerNames

  If MSettings.PlayerNames.Count = 0 Then
    Balloon.Warning(("Create at least one player."), tlbAdd)
  Endif

End

Public Sub Save()

  '   Settings.Reload()
  MSettings.PlayerNames = lbxPlayers.List
  '   Settings.Save()

End

Public Sub tlbRemove_Click()

  Dim sMessage As String

  If Not lbxPlayers.Text Then Return
  sMessage = Subst(("Remove '&1'?"), lbxPlayers.Text)
  Message.Title = ("Confirm")

  If Message.Error(sMessage, ("Remove"), ("Cancel")) = 1 Then
    lbxPlayers.Remove(lbxPlayers.Index)
  Endif

End

Public Sub tlbRemoveAll_Click()

  If lbxPlayers.Count = 0 Then Return
  Message.Title = ("Confirm")

  If Message.Error(("Remove all players?"), ("Remove All"), ("Cancel")) = 1 Then
    lbxPlayers.Clear
  Endif

End

Public Sub tlbAdd_Click()

  Dim hForm As New FPlayersEdit
  Dim sName As String

  sName = hForm.Run()
  If Not sName Then Return

  If lbxPlayers.Find(sName, gb.IgnoreCase) = -1 Then
    lbxPlayers.Add(sName)
  Else
    Balloon.Warning(("Player already exists."), Last)
  Endif

End

Public Sub tlbEdit_Click()

  Dim hForm As New FPlayersEdit
  Dim sName As String

  If Not lbxPlayers.Text Then Return
  sName = hForm.Run(lbxPlayers.Text)
  If Not sName Or lbxPlayers.Text == sName Then Return

  If lbxPlayers.Find(sName, gb.IgnoreCase) = -1 Then
    lbxPlayers.Remove(lbxPlayers.Index)
    lbxPlayers.Add(sName)
  Else
    Balloon.Warning(("Player already exists."), Last)
  Endif

End

Public Sub lbxPlayers_DblClick()

  tlbEdit_Click()

End
