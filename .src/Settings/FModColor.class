' Gambas class file

Private $aColorBgButton As New ColorButton[MColor.ColorNames.Length]
Private $aColorFgButton As New ColorButton[MColor.ColorNames.Length]
Private $aLabel As New Label[MColor.ColorNames.Length]

Static Public Sub _init()

  MColor.Load()

End

Public Sub _new()

  Dim i As Integer
  Dim hColorButton As ColorButton
  Dim hHBox As HBox
  Dim hLabel As Label

  For i = 0 To MColor.ColorNames.Max
    hHBox = New HBox(svwTheme)
    hHBox.Tag = i
    hHBox.Height = 4 * Desktop.Scale
    hHBox.Spacing = 4

    hColorButton = New ColorButton(hHBox) As "btnColorBg"
    hColorButton.Width = Desktop.Scale * 6
    hColorButton.Tag = i
    hColorButton.Color = MColor.GetBackground(i)
    $aColorBgButton[i] = hColorButton

    hColorButton = New ColorButton(hHBox) As "btnColorFg"
    hColorButton.Width = Desktop.Scale * 6
    hColorButton.Tag = i
    hColorButton.Color = MColor.GetForeground(i)
    $aColorFgButton[i] = hColorButton

    hLabel = New Label(hHBox)
    hLabel.Padding = 4
    hLabel.Expand = True
    hLabel.Text = MColor.ColorNames[i]
    hLabel.Background = $aColorBgButton[i].Color
    hLabel.Foreground = $aColorFgButton[i].Color
    $aLabel[i] = hLabel
  Next

End

Public Sub btnColorBg_Change()

  $aLabel[Last.Tag].Background = Last.Color

End

Public Sub btnColorFg_Change()

  $aLabel[Last.Tag].Foreground = Last.Color

End

Public Sub Save()

  Dim i As Integer

  For i = 0 To $aColorBgButton.Max
    MColor.SetBackground(i, $aColorBgButton[i].Color)
  Next

  For i = 0 To $aColorFgButton.Max
    MColor.SetForeground(i, $aColorFgButton[i].Color)
  Next

  MColor.Save()

End
