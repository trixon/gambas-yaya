' Gambas class file

Static Private $aTheme As String[] = ["yaya", "plainpaper", "darkness"]
Static Private $aThemeName As String[] = [("Yaya"), ("Plain paper"), ("Darkness")]

Private $aDefineButtons As New Button[MSettings.StyleNames.Length]
Private $aLabel As New Label[MSettings.StyleNames.Length]
Private $iIndex As Integer

Public Sub _new()

  Dim i As Integer
  Dim hButton As Button
  Dim hHBox As HBox
  Dim hLabel As Label

  cmbTheme.List = $aThemeName.Copy().Sort(gb.Language)
  cmbTheme.Add(("Select a theme"), 0)

  For i = 0 To MSettings.StyleNames.Max
    hHBox = New HBox(svwTheme)
    With hHBox
      .Tag = i
      .Height = 4 * Desktop.Scale
      .Spacing = 4
      .AutoResize = True
    End With

    hLabel = New Label(hHBox) As "lblDefine"
    With hLabel
      .AutoResize = True
      .Padding = 4
      .Expand = True
      .Text = MSettings.StyleNames[i]
      .Background = MSettings.GetBackground(i)
      .Foreground = MSettings.GetForeground(i)
      .Font = MSettings.GetFont(i)
    End With
    $aLabel[i] = hLabel

    hButton = New Button(hHBox) As "btnDefine"
    With hButton
      .Width = Desktop.Scale * 12
      .Text = ("Set...")
    End With
    $aDefineButtons[i] = hButton
  Next

End

Public Sub Form_Open()

  cbxShowIndicators.Value = MSettings.ShowIndicators
  cbxShowMaxCol.Value = MSettings.ShowMaxCol
  cbxShowTopCol.Value = MSettings.ShowTopCol
  cbxShowSymbols.Value = MSettings.ShowSymbols

End

Public Sub Save()

  Dim i As Integer

  '   Settings.Reload()
  MSettings.ShowIndicators = cbxShowIndicators.Value
  MSettings.ShowMaxCol = cbxShowMaxCol.Value
  MSettings.ShowTopCol = cbxShowTopCol.Value
  MSettings.ShowSymbols = cbxShowSymbols.Value

  For i = 0 To MSettings.StyleNames.Max
    MSettings.SetBackground(i, $aLabel[i].Background)
    MSettings.SetForeground(i, $aLabel[i].Foreground)
    MSettings.SetFont(i, $aLabel[i].Font)
  Next

  '   Settings.Save()

End

Public Sub btnDefine_Click()

  $iIndex = Last.Parent.Tag
  mnuPopup.Popup()

End

Public Sub cmbTheme_Click()

  If cmbTheme.Index <= 0 Then Return

  '  $cCurrent = MTheme.ReadFile($aTheme[$aThemeName.Find(cmbTheme.Text)])
  '  RefreshEditor

End

Public Sub lblDefine_MouseDown()

  $iIndex = Last.Parent.Tag
  mnuPopup.Popup()

End

Public Sub mnuBackground_Click()

  $aLabel[$iIndex].Background = FColorChooser.Run($aLabel[$iIndex].Background)

End

Public Sub mnuForeground_Click()

  $aLabel[$iIndex].Foreground = FColorChooser.Run($aLabel[$iIndex].Foreground)

End

Public Sub mnuFont_Click()

  Dim sFont As String

  sFont = FFontChooser.Run($aLabel[$iIndex].Font.ToString())
  If Not sFont Then Return
  $aLabel[$iIndex].Font = Font[sFont]

End
