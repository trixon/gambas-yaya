' Gambas class file

Inherits GridView
Property MaxCol As Boolean
Property TopCol As Boolean

Private Const ALPHA As Integer = &H44000000
Private Const ROW_HEIGHT_MARGIN As Integer = 6
Private Const COL_WIDTH_MARGIN As Integer = 32
Private Const COL_WIDTH_TEXT As Integer = 128
Private Const COL_WIDTH_PLAYERS As Integer = 96
Private $bMaxCol As Boolean
Private $bTopCol As Boolean
Private $hGameDef As CGameDef

Public Sub _new(hGameDef As CGameDef)

  Dim iMaxCol As Integer = MSettings.Players + 2
  Dim iMaxRow As Integer = hGameDef.Rows.Count - 1
  Dim iCol As Integer
  Dim iRow As Integer

  $hGameDef = hGameDef

  With Me
    .Columns.Count = iMaxCol + 1
    .Rows.Count = iMaxRow + 1
    .Columns.Width = COL_WIDTH_PLAYERS
    .Border = Border.None
    .Background = MSettings.GetBackground(MSettings.Score)
    .Background = &DD000000
    .Foreground = MSettings.GetForeground(MSettings.Score)
    .AutoResize = False
    .ScrollBar = Scroll.None
  End With

  Me.Columns[0].Width = COL_WIDTH_TEXT

End

Public Sub NewGame()

  ResetLayout()
  InitHeaderRow()

End

Private Sub ResetLayoutByCell(Row As Integer, Column As Integer)

  Dim hGameRow As CGameRow
  Dim hCell As _GridView_Cell

  If Row > $hGameDef.Rows.Length - 1 Then
    hGameRow = New CGameRow
  Else
    hGameRow = $hGameDef.Rows[Row]
  Endif

  hCell = Me[Row, Column]
  hCell.Padding = 8
  hCell.Alignment = Align.Right
  hCell.Background = MSettings.GetBackground(MSettings.Score) Or ALPHA

  hCell.Font = MSettings.GetFont(MSettings.Score)
  If Column = 0 Then
    hCell.Background = MSettings.GetBackground(MSettings.Header) Or ALPHA
    hCell.Foreground = MSettings.GetForeground(MSettings.Header)
    hCell.Font = MSettings.GetFont(MSettings.Header)
    hCell.Alignment = Align.Left
  Else If Column = 1 Then
    If Row = 0 Then
      hCell.Text = ("Max")
    Else
      hCell.Text = hGameRow.Max
    Endif
  Else If Column = 2 Then
    If Row = 0 Then
      hCell.Text = ("Top")
    Else
      hCell.Text = hGameRow.Lim
    Endif
  Else
    If Row = 0 Then
      hCell.Text = MSettings.PlayerSelection[Column - 3]
    Endif
    If hGameRow.IsCounter Then
      hCell.Text = "0"
    Endif
  Endif

  If Row = 0 Then
    hCell.Alignment = Align.Center
  Endif

  If hGameRow.IsSum Or hGameRow.IsCounter Then
    hCell.Background = MSettings.GetBackground(MSettings.Sum) Or ALPHA
    hCell.Foreground = MSettings.GetForeground(MSettings.Sum)
    hCell.Font = MSettings.GetFont(MSettings.Sum)
  Endif

End

Private Sub InitHeaderRow()
  'TODO: Don't asume header row = 0

  Dim iMaxCol As Integer = MSettings.Players + 2
  Dim iCol As Integer
  Dim hCell As _GridView_Cell

  For iCol = 0 To iMaxCol
    hCell = Me[0, iCol]

    hCell.Background = MSettings.GetBackground(MSettings.Name)
    hCell.Foreground = MSettings.GetForeground(MSettings.Name)
    hCell.Font = MSettings.GetFont(MSettings.Name)

    If iCol = 0 Then
      hCell.Text = MGameLoader.TextById($hGameDef.Id)
      hCell.Font.Bold = False
    Endif

  Next

End

Public Sub ShowIndicators(Show As Boolean, PlayerIndex As CCircularInt, CurrentPlayer As CPlayer, Values As Integer[])

  Dim iRow As Integer
  Dim iBackground As Integer
  Dim iForeground As Integer
  Dim hFont As Font
  Dim sText As String
  Dim hGameRow As CGameRow
  Dim hCell As _GridView_Cell

  For iRow = 0 To $hGameDef.Rows.Max
    hGameRow = $hGameDef.Rows[iRow]
    hCell = Me[iRow, PlayerIndex.Value + 3]
    If hGameRow.IsPlayable And Values[iRow] > 0 And CurrentPlayer.Playable[iRow] Then
      hCell.Text = Values[iRow]
    Endif
    If Show Then
    Else
      iBackground = MSettings.GetBackground(MSettings.Score)
      iForeground = MSettings.GetForeground(MSettings.Score)
      hFont = MSettings.GetFont(MSettings.Score)
    Endif

  Next

End

Public Sub ShowTextAsSymbols(Value As Boolean)

  Dim iMaxRow As Integer = $hGameDef.Rows.Count - 1
  Dim iRow As Integer
  Dim hGameRow As CGameRow
  Dim hCell As _GridView_Cell
  Dim hFont As Font

  For iRow = 1 To iMaxRow
    hCell = Me[iRow, 0]

    If iRow > $hGameDef.Rows.Length - 1 Then
      hGameRow = New CGameRow
    Else
      hGameRow = $hGameDef.Rows[iRow]
    Endif

    If hGameRow.IsBonus Or hGameRow.IsSum Then
      hFont = MSettings.GetFont(MSettings.Sum)
    Else
      hFont = MSettings.GetFont(MSettings.Header)
    Endif

    If Value Then
      hCell.Text = hGameRow.Symbol
      hCell.Font.Size = hFont.Size + 4
    Else
      hCell.Text = hGameRow.Text
    Endif
  Next

End

Public Sub RecalcWidth()

  Dim iWidth As Integer
  Dim iMaxCol As Integer = MSettings.Players + 2
  Dim iMaxRow As Integer = $hGameDef.Rows.Count - 1
  Dim iCol As Integer
  Dim iRow As Integer
  Dim hFont As Font
  Dim iColumnWidth As Integer
  Dim sText As String
  Dim hGameRow As CGameRow

  For iCol = 0 To iMaxCol
    If iCol = 0 Then
      'First column, game name and headers
      iColumnWidth = 0
      For iRow = 0 To iMaxRow
        hFont = Me[iRow, iCol].Font
        sText = Me[iRow, iCol].Text
        iColumnWidth = Max(iColumnWidth, hFont.TextWidth(sText))
      Next
    Else If iCol = 1
      'Second column, max values.
      If $bMaxCol Then
        sText = "8888"
        'iColumnWidth = hFont.TextWidth(sText)
        iColumnWidth = 0
        For iRow = 0 To iMaxRow
          hFont = Me[iRow, iCol].Font
          iColumnWidth = Max(iColumnWidth, hFont.TextWidth(sText))
        Next
      Else
        iColumnWidth = - COL_WIDTH_MARGIN
      Endif
    Else If iCol = 2
      'Third column, top values.
      If $bTopCol Then
        sText = "8888"
        iColumnWidth = 0
        For iRow = 0 To iMaxRow
          hFont = Me[iRow, iCol].Font
          iColumnWidth = Max(iColumnWidth, hFont.TextWidth(sText))
        Next
      Else
        iColumnWidth = - COL_WIDTH_MARGIN
      Endif
    Else
      'Remaining columns, player names, scores, and sums.
      iColumnWidth = 0
      For iRow = 0 To iMaxRow
        hFont = Me[iRow, iCol].Font

        If iRow = 0 Then
          sText = "WWWWWWW"
        Else
          sText = "8888"
        Endif

        iColumnWidth = Max(iColumnWidth, hFont.TextWidth(sText))
      Next
    Endif
    Me.Columns[iCol].Width = iColumnWidth + COL_WIDTH_MARGIN
  Next

  iWidth = Me.Columns[0].Width + Me.Columns[1].Width + Me.Columns[2].Width + MSettings.Players * (iColumnWidth + COL_WIDTH_MARGIN)
  Me.Width = iWidth

End

Public Sub ResetLayout()

  Dim iMaxCol As Integer = MSettings.Players + 2
  Dim iCol As Integer

  For iCol = 0 To iMaxCol
    ResetLayoutByColumn(iCol)
  Next

End

Public Sub ResetLayoutByColumn(Column As Integer)

  Dim iMaxRow As Integer = $hGameDef.Rows.Count - 1
  Dim iRow As Integer

  For iRow = 0 To iMaxRow
    ResetLayoutByCell(iRow, Column)
  Next

  InitHeaderRow()

End

Public Sub ApplyLayout()

  Dim iMaxRow As Integer = $hGameDef.Rows.Count - 1
  Dim iRow As Integer
  Dim hGameRow As CGameRow
  Dim iFontHeight As Integer

  ResetLayout()
  InitHeaderRow()

  For iRow = 0 To iMaxRow
    If iRow > $hGameDef.Rows.Length - 1 Then
      hGameRow = New CGameRow
    Else
      hGameRow = $hGameDef.Rows[iRow]
    Endif

    If iRow = 0 Then
      iFontHeight = MSettings.GetFont(MSettings.Name).Height
    Else If hGameRow.IsSum Or hGameRow.IsCounter
      iFontHeight = MSettings.GetFont(MSettings.Sum).Height
    Else If hGameRow.IsPlayable
      iFontHeight = MSettings.GetFont(MSettings.Score).Height
    Endif
    Me.Rows[iRow].Height = iFontHeight + ROW_HEIGHT_MARGIN
  Next

End

Public Sub HoverRow(Row As Integer, Column As Integer, Playable As Boolean)

  Dim hGameRow As CGameRow
  Dim iHover As Integer = &H555555

  ResetLayoutByColumn(0)
  ResetLayoutByColumn(Column)

  hGameRow = $hGameDef.Rows[Row]
  If hGameRow.IsPlayable And Playable Then
    Me[Row, 0].Background = MSettings.GetBackground(MSettings.Header) Or iHover Or ALPHA
    Me[Row, Column].Background = MSettings.GetBackground(MSettings.Score) Or iHover Or ALPHA
  Endif

End

Private Function MaxCol_Read() As Boolean

  Return $bMaxCol

End

Private Sub MaxCol_Write(Value As Boolean)

  $bMaxCol = Value

End

Private Function TopCol_Read() As Boolean

  Return $bTopCol

End

Private Sub TopCol_Write(Value As Boolean)

  $bTopCol = Value

End

Public Sub UpdateRolls(iPlayer As Integer, sRolls As String)

  Dim hGameRow As CGameRow
  Dim hCell As _GridView_Cell
  Dim iMaxRow As Integer = $hGameDef.Rows.Count - 1
  Dim iRow As Integer

  For iRow = 0 To iMaxRow
    If iRow > $hGameDef.Rows.Length - 1 Then
      hGameRow = New CGameRow
    Else
      hGameRow = $hGameDef.Rows[iRow]
    Endif
    If hGameRow.IsCounter Then
      Me[iRow, iPlayer + 3].Text = sRolls
    Endif
  Next

End
