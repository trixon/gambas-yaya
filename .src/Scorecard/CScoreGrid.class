' Gambas class file

Inherits GridView
Private Const ALPHA As Integer = &H44000000
Private Const ROW_HEIGHT_MARGIN As Integer = 6
Private Const COL_WIDTH_MARGIN As Integer = 32
Private Const COL_WIDTH_TEXT As Integer = 128
Private Const COL_WIDTH_MAXTOP As Integer = 48
Private Const COL_WIDTH_PLAYERS As Integer = 96
Private $hGameDef As CGameDef

Public Sub _new(hGameDef As CGameDef)

  Dim iMaxCol As Integer = MSettings.Players + 2
  Dim iMaxRow As Integer = hGameDef.Rows.Count - 1
  Dim iCol As Integer
  Dim iRow As Integer

  $hGameDef = hGameDef

  With Me
    .Columns.Count = iMaxCol + 1
    .Rows.Count = iMaxRow + 1
    .Columns.Width = COL_WIDTH_PLAYERS
    .Border = Border.None
    .Background = MSettings.GetBackground(MSettings.Score)
    .Background = &DD000000 '&FF000000
    .Foreground = MSettings.GetForeground(MSettings.Score)
    .AutoResize = False
    .ScrollBar = Scroll.None
  End With

  Me.Columns[0].Width = COL_WIDTH_TEXT

End

Public Sub NewGame()

  Dim iMaxCol As Integer = MSettings.Players + 2
  Dim iMaxRow As Integer = $hGameDef.Rows.Count - 1
  Dim iCol As Integer
  Dim iRow As Integer

  For iCol = 0 To iMaxCol
    For iRow = 0 To iMaxRow
      InitCell(iRow, iCol)
    Next
  Next

  InitHeaderRow()

End

Private Sub InitCell(Row As Integer, Column As Integer)

  Dim hGameRow As CGameRow
  Dim hCell As _GridView_Cell

  If Row > $hGameDef.Rows.Length - 1 Then
    hGameRow = New CGameRow
  Else
    hGameRow = $hGameDef.Rows[Row]
  Endif

  hCell = Me[Row, Column]
  hCell.Padding = 8
  hCell.Alignment = Align.Right
  hCell.Background = MSettings.GetBackground(MSettings.Score) Or ALPHA

  hCell.Font = MSettings.GetFont(MSettings.Score)
  If Column = 0 Then
    hCell.Background = MSettings.GetBackground(MSettings.Header) Or ALPHA
    hCell.Foreground = MSettings.GetForeground(MSettings.Header)
    hCell.Font = MSettings.GetFont(MSettings.Header)
    hCell.Alignment = Align.Left
  Else If Column = 1 Then
    If Row = 0 Then
      hCell.Text = ("Max")
    Else
      hCell.Text = hGameRow.Max
    Endif
  Else If Column = 2 Then
    If Row = 0 Then
      hCell.Text = ("Top")
    Else
      hCell.Text = hGameRow.Lim
    Endif
  Else
    If Row = 0 Then
      hCell.Text = MSettings.PlayerSelection[Column - 3]
    Endif
  Endif

  If Row = 0 Then
    hCell.Alignment = Align.Center
  Endif

  If hGameRow.IsSum Or hGameRow.IsCounter Then
    hCell.Background = MSettings.GetBackground(MSettings.Sum) Or ALPHA
    hCell.Foreground = MSettings.GetForeground(MSettings.Sum)
    hCell.Font = MSettings.GetFont(MSettings.Sum)
  Endif

End

Private Sub InitHeaderRow()

  Dim iMaxCol As Integer = MSettings.Players + 2
  Dim iCol As Integer
  Dim hCell As _GridView_Cell

  For iCol = 0 To iMaxCol
    hCell = Me[0, iCol]

    hCell.Background = MSettings.GetBackground(MSettings.Name)
    hCell.Foreground = MSettings.GetForeground(MSettings.Name)
    hCell.Font = MSettings.GetFont(MSettings.Name)

    If iCol = 0 Then
      hCell.Text = MGameLoader.TextById($hGameDef.Id)
      hCell.Font.Bold = False
    Endif

  Next

End

Public Sub ShowTopCol(Value As Boolean)

  If Value Then
    Me.Columns[2].Width = COL_WIDTH_MAXTOP
  Else
    Me.Columns[2].Width = 0
  Endif

End

Public Sub ShowMaxCol(Value As Boolean)

  If Value Then
    Me.Columns[1].Width = COL_WIDTH_MAXTOP
  Else
    Me.Columns[1].Width = 0
  Endif

End

Public Sub ShowTextAsSymbols(Value As Boolean)

  Dim iMaxRow As Integer = $hGameDef.Rows.Count - 1
  Dim iRow As Integer
  Dim hGameRow As CGameRow
  Dim hCell As _GridView_Cell
  Dim hFont As Font

  For iRow = 1 To iMaxRow
    hCell = Me[iRow, 0]

    If iRow > $hGameDef.Rows.Length - 1 Then
      hGameRow = New CGameRow
    Else
      hGameRow = $hGameDef.Rows[iRow]
    Endif

    If hGameRow.IsBonus Or hGameRow.IsSum Then
      hFont = MSettings.GetFont(MSettings.Sum)
    Else
      hFont = MSettings.GetFont(MSettings.Header)
    Endif

    If Value Then
      hCell.Text = hGameRow.Symbol
      hCell.Font.Size = hFont.Size + 4
    Else
      hCell.Text = hGameRow.Text
    Endif
  Next

End

Public Sub RecalcWidth()

  Dim iWidth As Integer
  Dim iMaxCol As Integer = MSettings.Players + 2
  Dim iMaxRow As Integer = $hGameDef.Rows.Count - 1
  Dim iCol As Integer
  Dim iRow As Integer
  Dim hFont As Font
  Dim iColumnWidth As Integer
  Dim sText As String
  Dim hGameRow As CGameRow

  For iCol = 0 To iMaxCol
    If iCol = 0 Then
      'First column, game name and headers
      iColumnWidth = 0
      For iRow = 0 To iMaxRow
        hFont = Me[iRow, iCol].Font
        sText = Me[iRow, iCol].Text
        iColumnWidth = Max(iColumnWidth, hFont.TextWidth(sText))
      Next
    Else If iCol = 1 Or iCol = 2
      'Second and third column, max and top values.
      sText = "8888"
      iColumnWidth = hFont.TextWidth(sText)
    Else
      'Remaining columns, player names, scores, and sums.
      iColumnWidth = 0
      For iRow = 0 To iMaxRow
        hFont = Me[iRow, iCol].Font

        If iRow > $hGameDef.Rows.Length - 1 Then
          hGameRow = New CGameRow
        Else
          hGameRow = $hGameDef.Rows[iRow]
        Endif

        If iRow = 0 Then
          sText = "WWWWWWWWW"
        Else
          sText = "8888"
        Endif

        iColumnWidth = Max(iColumnWidth, hFont.TextWidth(sText))
      Next
    Endif
    Me.Columns[iCol].Width = iColumnWidth + COL_WIDTH_MARGIN
  Next

  iWidth = Me.Columns[0].Width + Me.Columns[1].Width + Me.Columns[2].Width + MSettings.Players * (iColumnWidth + COL_WIDTH_MARGIN)
  Me.Width = iWidth

End

Public Sub ApplyLayout()

  Dim iMaxCol As Integer = MSettings.Players + 2
  Dim iMaxRow As Integer = $hGameDef.Rows.Count - 1
  Dim iCol As Integer
  Dim iRow As Integer
  Dim hGameRow As CGameRow
  Dim iFontHeight As Integer

  '   Me.Font = Font[MSettings.Font]
  For iCol = 0 To iMaxCol
    For iRow = 0 To iMaxRow
      InitCell(iRow, iCol)
    Next
  Next

  InitHeaderRow()

  For iRow = 0 To iMaxRow
    If iRow > $hGameDef.Rows.Length - 1 Then
      hGameRow = New CGameRow
    Else
      hGameRow = $hGameDef.Rows[iRow]
    Endif

    If iRow = 0 Then
      iFontHeight = MSettings.GetFont(MSettings.Name).Height
    Else If hGameRow.IsSum Or hGameRow.IsCounter
      iFontHeight = MSettings.GetFont(MSettings.Sum).Height
    Else If hGameRow.IsPlayable
      iFontHeight = MSettings.GetFont(MSettings.Score).Height
    Endif
    Me.Rows[iRow].Height = iFontHeight + ROW_HEIGHT_MARGIN
  Next

End

Public Sub HoverRow(iRow As Integer)

  Dim iMaxCol As Integer = MSettings.Players + 2
  Dim iCol As Integer
  Dim hCell As _GridView_Cell

  For iCol = 0 To iMaxCol
    hCell = Me[iRow, iCol]

    '     hCell.Background = Color.Green

  Next

End
