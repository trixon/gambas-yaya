' Gambas class file

Property Read Panel As Panel

Private $hLeftPanel As Panel
Private $hRightPanel As Panel
Private $hPanel As Panel
Private $hGrid As CScoreGrid
Private $hButton As Button
Private $hGameDef As CGameDef

Public Sub _new(hContainer As Container)

  $hPanel = New Panel(hContainer) As "Panel"

  With $hPanel
    .Arrangement = Arrange.Horizontal
    .Expand = True
  End With

End

Private Function Panel_Read() As Panel

  Return $hPanel

End

Private Sub GridInitHeaderRow()

  Dim iMaxCol As Integer = MSettings.Players + 2
  Dim iCol As Integer
  Dim hCell As _GridView_Cell

  For iCol = 0 To iMaxCol
    hCell = $hGrid[0, iCol]

    hCell.Background = Color.Lighter(Color.Gray)
    hCell.Background = &HFFFFDF
    hCell.Font = Font["Bold"]

    If iCol = 0 Then
      hCell.Text = MGameLoader.TextById($hGameDef.Id)
      hCell.Font = Font[""]
    Endif

    ' If MSettings.ShowSymbols Then
    '   hCell.Text = hGameRow.Symbol
    ' Else
    '   hCell.Text = hGameRow.Text
    ' Endif

  Next

End

Private Sub GridInitCell(Row As Integer, Column As Integer)

  Dim hGameRow As CGameRow
  Dim hCell As _GridView_Cell

  '  Debug Row; "x"; Column
  If Row > $hGameDef.Rows.Length - 1 Then
    hGameRow = New CGameRow
  Else
    hGameRow = $hGameDef.Rows[Row]
  Endif

  hCell = $hGrid[Row, Column]
  hCell.Padding = 8
  hCell.Alignment = Align.Right
  hCell.Font = Font["10"]

  If Column = 0 Then
    If MSettings.ShowSymbols Then
      hCell.Text = hGameRow.Symbol
    Else
      hCell.Text = hGameRow.Text
    Endif
    hCell.Background = Color.Orange
    hCell.Alignment = Align.Left
  Else If Column = 1 Then
    If Row = 0 Then
      hCell.Text = ("Max")
    Else
      hCell.Text = hGameRow.Max
    Endif
  Else If Column = 2 Then
    If Row = 0 Then
      hCell.Text = ("Top")
    Else
      hCell.Text = hGameRow.Lim
    Endif
  Else
    If Row = 0 Then
      '       hCell.Text = MSettings.PlayerSelection[Column - 3]
    Endif
  Endif

  If Row = 0 Then
    hCell.Alignment = Align.Center
  Endif

  If hGameRow.IsSum Or hGameRow.IsCounter Then
    hCell.Background = Color.Yellow
    hCell.Font = Font["Bold"]
  Endif

End

Public Sub NewGame()

  $hGameDef = MGameLoader.GameDefs[MSettings.GameId]

  GridInit()
  Settings()
  CalcGridWidth()

End

Public Sub Settings()

  If MSettings.ShowMaxCol Then
    $hGrid.Columns[1].Width = 48
  Else
    $hGrid.Columns[1].Width = 0
  Endif

  If MSettings.ShowTopCol Then
    $hGrid.Columns[2].Width = 48
  Else
    $hGrid.Columns[2].Width = 0
  Endif

  'UpdateHeaderText()

End

Private Sub CalcGridWidth()

  Dim iWidth As Integer

  iWidth = $hGrid.Columns[0].Width + $hGrid.Columns[1].Width + $hGrid.Columns[2].Width + MSettings.Players * 52
  $hGrid.Width = iWidth

End

Private Sub GridInit()

  Dim iMaxCol As Integer = MSettings.Players + 2
  Dim iMaxRow As Integer = $hGameDef.Rows.Count - 1
  Dim iCol As Integer
  Dim iRow As Integer

  Try $hLeftPanel.Delete
  Try $hGrid.Delete
  Try $hRightPanel.Delete

  $hLeftPanel = New Panel($hPanel)
  $hGrid = New CScoreGrid($hPanel) As "GridView"
  $hRightPanel = New Panel($hPanel)

  $hLeftPanel.Expand = True
  $hRightPanel.Expand = True

  With $hGrid
    .Columns.Count = iMaxCol + 1
    .Rows.Count = iMaxRow + 1
    .Columns.Width = 52
    .Width = 400
    .Height = 200
    .Border = Border.None
    .Background = &HFFFFDF
    .AutoResize = False
  End With

  $hGrid.Columns[0].Width = 128

  For iCol = 0 To iMaxCol
    For iRow = 0 To iMaxRow
      GridInitCell(iRow, iCol)
    Next
  Next

  GridInitHeaderRow()

End