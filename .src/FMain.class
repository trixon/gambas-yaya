' Gambas class file

Public Const KEY_FULL_SCREEN As String = "Window/FullScreen"
Public Const KEY_PLAYER_NAMES As String = "Players"
Private $hGamePanel As CGamePanel

Public Sub _new()

  MGameLoader.LoadAll()
  $hGamePanel = New CGamePanel(pnlMain)

End

Public Sub Action_Activate(key As String) As Boolean

  Select Case key
    Case "about"
      About()

    Case "new"
      NewGame()

    Case "players"
      Players()

    Case "settings"
      SettingsDialog()

    Case "dice4"
      $hGamePanel.DicePanel.DiceCount = 4

    Case "dice5"
      $hGamePanel.DicePanel.DiceCount = 5

    Case "dice6"
      $hGamePanel.DicePanel.DiceCount = 6

    Case "full_screen"
      Me.FullScreen = Not Me.FullScreen
      UpdateToolBarBackground()

    Case "quit"
      Me.Close

    Default
      Print "Unknown action: " & key

  End Select

End

Private Sub UpdateToolBarBackground()

  If Me.FullScreen Then
    Panel1.Background = pnlMain.Background
  Else
    Panel1.Background = Color.Default
  Endif

End

Public Sub Form_Open()

  Settings.Read(Me)
  Me.FullScreen = Settings[KEY_FULL_SCREEN]
  UpdateToolBarBackground()

  $hGamePanel.Open()
  Form_Resize()

End

Public Sub Form_Close()

  $hGamePanel.Close()
  Settings.Write(Me)
  Settings[KEY_FULL_SCREEN] = Me.FullScreen

End

Public Sub Form_Resize()

  MGuiUtils.CenterInParent($hGamePanel.Panel)

End

Public Sub Form_KeyPress()

  If Key.Code = Key.Esc Then
    Me.Close()
  Else If Key.Code = Key["Q"] And If Key.Control
    Me.Close()
  Else If Key.Code = Key["N"] And If Key.Control
    NewGame()
  Endif

End

Private Sub About()

  Dim hForm As New FAbout

  With hForm
    .AppAuthor = "Patrik Karlsson"
    .AppCopyright = ("Copyright Â© 2014 Patrik Karlsson")
    .AppDescription = ("Yet Another Yahtzee")
    .AppLogo = Picture.Load("logo.png")
  End With

  hForm.Run()

End

Private Sub NewGame()

  Dim hForm As New FNewGame

  If hForm.Run() Then
    $hGamePanel.NewGame()
  Endif

End

Private Sub Players()

  Dim hForm As New FPlayers

  hForm.Run()

End

Private Sub SettingsDialog()

  Dim hForm As New FSettings

  If Me.FullScreen Then
    Me.FullScreen = False
    hForm.Run()
    Me.FullScreen = True
  Else
    hForm.Run()

  Endif

End

Public Sub ToolButton4_Click()

  mnuMain.Popup()

End
