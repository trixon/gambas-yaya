' Gambas class file

Public Const KEY_FULL_SCREEN As String = "Window/FullScreen"
Private $hGamePanel As CGamePanel

Public Sub _new()

  MGameLoader.LoadAll()
  $hGamePanel = New CGamePanel(Me)

End

Public Sub Action_Activate(key As String) As Boolean

  Select Case key
    Case "about"
      About()

    Case "new"
      NewGame()

    Case "settings"
      SettingsDialog()

    Case "full_screen"
      Me.FullScreen = Not Me.FullScreen

    Case "quit"
      Me.Close

    Default
      Print "Unknown action: " & key

  End Select

End

Public Sub Form_Open()

  Settings.Read(Me)
  Me.FullScreen = Settings[KEY_FULL_SCREEN]

  $hGamePanel.Open()
  $hGamePanel.NewGame()

End

Public Sub Form_Close()

  $hGamePanel.Close()
  Settings.Write(Me)
  Settings[KEY_FULL_SCREEN] = Me.FullScreen

End

Public Sub Form_KeyPress()

  If Key.Code = Key.Esc Then
    Me.Close()
  Else If Key.Code = Key["Q"] And If Key.Control
    Me.Close()
  Else If Key.Code = Key["N"] And If Key.Control
    NewGame()
  Else If Key.Code = Key["Z"] And If Key.Control And If mnuUndo.Enabled
    Undo()
  Else If Key.Code = Key["F11"]
    Me.FullScreen = Not Me.FullScreen
  Endif

End

Private Sub About()

  Dim hForm As New FAbout

  With hForm
    .AppAuthor = "Patrik Karlsson"
    .AppCopyright = ("Copyright Â© 2014 Patrik Karlsson")
    .AppDescription = ("Yet Another Yahtzee")
    .AppLogo = Picture.Load("logo.png")
  End With

  hForm.Run()

End

Private Sub NewGame()

  Dim hForm As New FNewGame

  If MSettings.PlayerNames.Count = 0 Then
    SettingsDialog()
  Endif

  If MSettings.PlayerNames.Count > 0 Then
    If hForm.Run() Then
      $hGamePanel.NewGame()
    Endif
  Endif

End

Public Sub Undo()

  Debug

End

Private Sub SettingsDialog()

  Dim hForm As New FSettings

  If hForm.Run() Then $hGamePanel.Settings()

End

Public Sub Form_Menu()

  mnuMain.Popup

End

Public Sub mnuPopup_Click()

  Debug Last.Tag
  NewGame

End
