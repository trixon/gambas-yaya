' Gambas class file

Public Const KEY_FULL_SCREEN As String = "Window/FullScreen"
Private $hGamePanel As CGamePanel

Public Sub _new()

  $hGamePanel = New CGamePanel(pnlMain)

End

Public Sub Action_Activate(key As String) As Boolean

  Select Case key
    Case "new"
      NewGame()

    Case "dice4"
      $hGamePanel.DicePanel.DiceCount = 4

    Case "dice5"
      $hGamePanel.DicePanel.DiceCount = 5

    Case "dice6"
      $hGamePanel.DicePanel.DiceCount = 6

    Case "full_screen"
      Me.FullScreen = Not Me.FullScreen
      UpdateToolBarBackground()

    Case "quit"
      Me.Close

    Default
      Print "Unknown action: " & key

  End Select

End

Private Sub UpdateToolBarBackground()

  If Me.FullScreen Then
    tlbMain.Background = pnlMain.Background
  Else
    tlbMain.Background = Color.Default
  Endif

End

Public Sub Form_Open()

  Settings.Read(Me)
  Me.FullScreen = Settings[KEY_FULL_SCREEN]
  UpdateToolBarBackground()

  $hGamePanel.Open()
  Form_Resize()

End

Public Sub Form_Close()

  $hGamePanel.Close()
  Settings.Write(Me)
  Settings[KEY_FULL_SCREEN] = Me.FullScreen

End

Public Sub Form_Resize()

  MGuiUtils.CenterInParent($hGamePanel.Panel)

End

Public Sub Form_KeyPress()

  If Key.Code = Key.Esc Then
    Me.Close()
  Else If Key.Code = Key["Q"] And If Key.Control
    Me.Close()
  Endif

End

Private Sub NewGame()

  Dim hNewGame As New FNewGame

  hNewGame.ShowModal

End
