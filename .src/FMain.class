' Gambas class file

Public Const KEY_FULL_SCREEN As String = "Window/FullScreen"
Public Const KEY_MENU_BAR As String = "Window/MenuBar"
Private $hGamePanel As CGamePanel

Public Sub _new()

  $hGamePanel = New CGamePanel(pnlMain)

End

Public Sub Action_Activate(key As String) As Boolean

  Select Case key
    Case "dice4"
      $hGamePanel.DicePanel.DiceCount = 4

    Case "dice5"
      $hGamePanel.DicePanel.DiceCount = 5

    Case "dice6"
      $hGamePanel.DicePanel.DiceCount = 6

    Case "full_screen"
      Me.FullScreen = mnuFullScreen.Checked

    Case "menu_bar_hide"
      MenuVisibility(mnuHideMenuBar.Checked)

    Case "menu_bar_show"
      MenuVisibility(True)

    Case "quit"
      Me.Close

    Default
      Print "Unknown action: " & key

  End Select

End

Public Sub Form_Open()

  Settings.Read(Me)
  Me.FullScreen = Settings[KEY_FULL_SCREEN]
  mnuFullScreen.Checked = Me.FullScreen
  MenuVisibility(Settings[KEY_MENU_BAR, True])

  InitMenuShortcuts()

End

Public Sub Form_Close()

  Settings.Write(Me)
  Settings[KEY_FULL_SCREEN] = Me.FullScreen
  Settings[KEY_MENU_BAR] = Me.Menus.Visible

End

Public Sub Form_Resize()

  MGuiUtils.CenterInParent($hGamePanel.Panel)

End

Public Sub Form_Menu()

  If Not Me.Menus.Visible Then
    mnuPopupMain.Popup
  Endif

End

Private Sub InitMenuShortcuts()

  If Application.Env["GB_GUI"] Begins "gb.gtk" Then
    mnuFullScreen.Shortcut = "F11"
  Endif

End

Private Sub MenuVisibility(bVisible As Boolean)

  Me.Menus.Visible = bVisible
  mnuHideMenuBar.Checked = bVisible

  Form_Resize()

End

Public Sub Form_KeyPress()

  If Key.Code = Key.Esc Then Me.Close()

End
