' Gambas class file

Public Sub Run() As Boolean

  Return Not Me.ShowModal()

End

Public Sub btnOK_Click()

  MSettings.PlayerNames = lbxPlayers.List
  Me.Close(True)

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub Form_Open()

  lbxPlayers.List = MSettings.PlayerNames

End

Public Sub tlbRemove_Click()

  Dim sMessage As String

  If Not lbxPlayers.Text Then Return
  sMessage = Subst(("Remove '&1'?"), lbxPlayers.Text)
  Message.Title = ("Confirm")

  If Message.Error(sMessage, ("Remove"), ("Cancel")) = 1 Then
    lbxPlayers.Remove(lbxPlayers.Index)
  Endif

End

Public Sub tlbRemoveAll_Click()

  If lbxPlayers.Count = 0 Then Return
  Message.Title = ("Confirm")

  If Message.Error(("Remove all players?"), ("Remove All"), ("Cancel")) = 1 Then
    lbxPlayers.Clear
  Endif

End

Public Sub tlbAdd_Click()

  Dim hForm As New FPlayersEdit
  Dim sName As String

  sName = hForm.Run()
  If Not sName Then Return

  If lbxPlayers.Find(sName, gb.IgnoreCase) = -1 Then
    lbxPlayers.Add(sName)
  Else
    Balloon.Warning(("Player already exists."), Last)
  Endif

End

Public Sub tlbEdit_Click()

  Dim hForm As New FPlayersEdit
  Dim sName As String

  If Not lbxPlayers.Text Then Return
  sName = hForm.Run(lbxPlayers.Text)
  If Not sName Or lbxPlayers.Text == sName Then Return

  If lbxPlayers.Find(sName, gb.IgnoreCase) = -1 Then
    lbxPlayers.Remove(lbxPlayers.Index)
    lbxPlayers.Add(sName)
  Else
    Balloon.Warning(("Player already exists."), Last)
  Endif

End

Public Sub lbxPlayers_DblClick()

  tlbEdit_Click()

End
