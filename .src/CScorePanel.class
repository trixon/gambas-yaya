' Gambas class file

Property Read Panel As Panel

Private $hPanel As Panel
Private $hGridView As GridView
Private $hButton As Button
Private $hGameDef As CGameDef

Public Sub _new(hContainer As Container)

  $hPanel = New Panel(hContainer) As "Panel"

  With $hPanel
    .Arrangement = Arrange.Horizontal
    .Background = &HFFFFDF
    .Expand = True
  End With

  ' $hGameDef = MGameLoader.GameDefs["default"]
  ' $hGameDef.Debug
  ' $hGridView = New GridView($hPanel) As "GridView"
  ' With $hGridView
  '   .Columns.Count = MSettings.Players + 1
  '   .Rows.Count = $hGameDef.Rows.Count
  '   .Columns.Width = 52
  '   .W = 400
  '   .H = 200
  '   .Border = Border.None
  '   .Background = &HFFFFDF
  ' End With

End

Private Function Panel_Read() As Panel

  Return $hPanel

End

Private Sub GridInitHeader()

  Dim iMaxCol As Integer = MSettings.Players + 2
  Dim iCol As Integer
  Dim hCell As _GridView_Cell

  For iCol = 0 To iMaxCol
    hCell = $hGridView[0, iCol]

    hCell.Background = Color.Lighter(Color.Gray)
    hCell.Background = &HFFFFDF
    hCell.Font = Font["Bold"]

    If iCol = 0 Then
      hCell.Text = MGameLoader.TextById($hGameDef.Id)
      hCell.Font = Font[""]
    Endif

  Next

End

Private Sub GridInitCell(Row As Integer, Column As Integer)

  Dim hGameRow As CGameRow
  Dim hCell As _GridView_Cell

  '  Debug Row; "x"; Column
  If Row > $hGameDef.Rows.Length - 1 Then
    hGameRow = New CGameRow
  Else
    hGameRow = $hGameDef.Rows[Row]
  Endif

  hCell = $hGridView[Row, Column]
  hCell.Padding = 4
  hCell.Alignment = Align.Right

  If Column = 0 Then
    hCell.Text = hGameRow.Text
    hCell.Background = Color.Orange
    hCell.Alignment = Align.Left
  Else If Column = 1 Then
    If Row = 0 Then
      hCell.Text = ("Max")
    Else
      hCell.Text = hGameRow.Max
    Endif
  Else If Column = 2 Then
    If Row = 0 Then
      hCell.Text = ("Top")
    Else
      hCell.Text = hGameRow.Lim
    Endif
  Else
    If Row = 0 Then
      hCell.Text = MSettings.PlayerSelection[Column - 3]
    Endif
  Endif

  If Row = 0 Then
    hCell.Alignment = Align.Center
  Endif

  If hGameRow.IsSum Or hGameRow.IsCounter Then
    hCell.Background = Color.Yellow
    hCell.Font = Font["Bold"]
  Endif

End

Public Sub NewGame()

  $hGameDef = MGameLoader.GameDefs[MSettings.GameId]

  GridInit()
  Settings()
  CalcGridWidth()

End

Public Sub Settings()

  If MSettings.ShowMaxCol Then
    $hGridView.Columns[1].Width = 48
  Else
    $hGridView.Columns[1].Width = 0
  Endif

End

Private Sub CalcGridWidth()

  Dim iWidth As Integer

  iWidth = $hGridView.Columns[0].Width + $hGridView.Columns[1].Width + $hGridView.Columns[2].Width + MSettings.Players * 52
  $hGridView.Width = iWidth

End

Private Sub GridInit()

  Dim iMaxCol As Integer = MSettings.Players + 2
  Dim iMaxRow As Integer = $hGameDef.Rows.Count - 1
  Dim iCol As Integer
  Dim iRow As Integer

  Try $hGridView.Delete
  $hGridView = New GridView($hPanel) As "GridView"

  With $hGridView
    .Columns.Count = iMaxCol + 1
    .Rows.Count = iMaxRow + 1
    .Columns.Width = 52
    .Width = 400
    .Height = 200
    .Border = Border.None
    .Background = &HFFFFDF
  End With

  $hGridView.Columns[0].Width = 128

  For iCol = 0 To iMaxCol
    For iRow = 0 To iMaxRow
      GridInitCell(iRow, iCol)
    Next
  Next

  GridInitHeader()

End
